(define leap
	(
		$1
		$2
		$1
		(verify not-friend?)
		add
	)
)

(define king-shift
	(
		$1
		(verify not-friend?)
		(set-attribute never-moved? false)
		add
	)
)

(define king-shift2
	(
		$1
		$2
		(verify not-friend?)
		(set-attribute never-moved? false)
		add
	)
)

(define slide
	(
		$1
		(while empty? add $1)
		(verify not-friend?)
		add
	)
)

(define slide2
	(
		$1
		$2
		(while empty? add $1 $2)
		(verify not-friend?)
		add
	)
)

(define rook-slide
	(
		$1
		(while empty? (set-attribute never-moved? false) add $1)
		(verify not-friend?)
		(set-attribute never-moved? false)
		add
	)
)

(define Pawn-add
	(if (in-zone? promotion-zone) (add Knight Bishop Rook Queen) else add)
)

(define Pawn-move
	(
		$1
		(verify empty?)
		(Pawn-add)
		(verify (in-zone? third-rank))
		$1
		(verify empty?)
		add
	)
)

(define Pawn-capture
	(
		$1
		$2
		(verify enemy?)
		(Pawn-add)
	)
)

(define En-Passant
	(
		$1
		(verify enemy?)
		(verify last-to?)
		(verify (or (piece? UpPawn) (piece? DownPawn)))
		capture
		$2
		to
		$2
		(verify last-from?)
		add
	)
)

(define O-O-S
	(
		(verify never-moved?)
		e
		(verify empty?)
		e
		(verify empty?)
		cascade
		e
		(verify (and friend? (piece? Rook) never-moved?))
		from
		back
		(verify not-attacked?)
		e
		(verify not-attacked?)
		to
		(set-attribute never-moved? false)
		e
		(set-attribute never-moved? false)
		add
	)
)

(define O-O-L
	(
		(verify never-moved?)
		e
		(verify empty?)
		e
		(verify empty?)
		cascade
		e
		(verify empty?)
		e
		(verify (and friend? (piece? Rook) never-moved?))
		from
		back
		(verify not-attacked?)
		e
		(verify not-attacked?)
		to
		(set-attribute never-moved? false)
		e
		(set-attribute never-moved? false)
		add
	)
)

(define O-O-O-S
	(
		(verify never-moved?)
		w
		(verify empty?)
		w
		(verify empty?)
		cascade
		w
		(verify empty?)
		w
		(verify (and friend? (piece? Rook) never-moved?))
		from
		back
		(verify not-attacked?)
		w
		(verify not-attacked?)
		to
		(set-attribute never-moved? false)
		w
		(set-attribute never-moved? false)
		add
	)
)

(define O-O-O-L
	(
		(verify never-moved?)
		w
		(verify empty?)
		w
		(verify empty?)
		cascade
		w
		(verify empty?)
		w
		(verify empty?)
		w
		(verify (and friend? (piece? Rook) never-moved?))
		from
		back
		(verify not-attacked?)
		w
		(verify not-attacked?)
		to
		(set-attribute never-moved? false)
		w
		(set-attribute never-moved? false)
		add
	)
)

(define Board3
	(grid
		(start-rectangle 16 16 47 47)
		(dimensions
			("a/b/c/d/e/f/g/h" (0 32))
			("1/2/3/4/5/6/7/8/9/10/11/12/13/14/15/16/17/18/19/20/21" (32 0))
		)
		(directions (n 0 1) (e 1 0) (s 0 -1) (w -1 0))
	)
	(zone
		(name promotion-zone)
		(players Red Green Blue)
		(positions a4 b4 c4 d4 e4 f4 g4 h4 a11 b11 c11 d11 e11 f11 g11 h11 a18 b18 c18 d18 e18 f18 g18 h18)
	)
	(zone
		(name third-rank)
		(players Red)
		(positions a2 b2 c2 d2 e2 f2 g2 h2 a6 b6 c6 d6 e6 f6 g6 h6)
	)
	(zone
		(name third-rank)
		(players Green)
		(positions a9 b9 c9 d9 e9 f9 g9 h9 a13 b13 c13 d13 e13 f13 g13 h13)
	)
	(zone
		(name third-rank)
		(players Blue)
		(positions a16 b16 c16 d16 e16 f16 g16 h16 a20 b20 c20 d20 e20 f20 g20 h20)
	)
)

(define Board4
	(grid
		(start-rectangle 16 16 47 47)
		(dimensions
			("a/b/c/d/e/f/g/h" (0 32))
			("1/2/3/4/5/6/7/8/9/10/11/12/13/14" (32 0))
		)
		(directions (n 0 1) (e 1 0) (s 0 -1) (w -1 0))
	)
	(grid
		(start-rectangle 16 320 47 351)
		(dimensions
			("a/b/c/d/e/f/g/h" (0 32))
			("15/16/17/18/19/20/21/22/23/24/25/26/27/28" (32 0))
		)
		(directions (n 0 1) (e 1 0) (s 0 -1) (w -1 0))
	)
	(zone
		(name promotion-zone)
		(players Red Yellow Green Blue)
		(positions a4 b4 c4 d4 e4 f4 g4 h4 a11 b11 c11 d11 e11 f11 g11 h11
			a18 b18 c18 d18 e18 f18 g18 h18 a25 b25 c25 d25 e25 f25 g25 h25)
	)
	(zone
		(name third-rank)
		(players Red)
		(positions a2 b2 c2 d2 e2 f2 g2 h2 a6 b6 c6 d6 e6 f6 g6 h6)
	)
	(zone
		(name third-rank)
		(players Yellow)
		(positions a9 b9 c9 d9 e9 f9 g9 h9 a13 b13 c13 d13 e13 f13 g13 h13)
	)
	(zone
		(name third-rank)
		(players Green)
		(positions a16 b16 c16 d16 e16 f16 g16 h16 a20 b20 c20 d20 e20 f20 g20 h20)
	)
	(zone
		(name third-rank)
		(players Blue)
		(positions a23 b23 c23 d23 e23 f23 g23 h23 a27 b27 c27 d27 e27 f27 g27 h27)
	)
	(links n (a14 a15) (b14 b15) (c14 c15) (d14 d15) (e14 e15) (f14 f15) (g14 g15) (h14 h15))
	(links s (a15 a14) (b15 b14) (c15 c14) (d15 d14) (e15 e14) (f15 f14) (g15 g14) (h15 h14))
)

(define Up-Pawn
	(help "Up-Pawn: moves forward, captures forward-diagonally")
	(description "Up-Pawn:\Moves forward by one field, can also move forward two fields from its starting position. It captures by moving one field forward and one field sideward. When it arrives at the starting position of the opponents non-pawns (marked by a thin frame), it promotes to anything except a king.")
	(image Red "images\Chess\RPawn.bmp" Yellow "images\Chess\YPawn.bmp" Green "images\Chess\GPawn.bmp" Blue "images\Chess\BPawn.bmp")
	(moves
		(Pawn-capture n e)
		(Pawn-capture n w)
		(Pawn-move n)
		(En-Passant e n)
		(En-Passant w n)
	)
)
(define Down-Pawn
	(help "Down-Pawn: moves backward, captures backward-diagonally")
	(description "Down-Pawn:\Moves backward by one field, can also move backward two fields from its starting position. It captures by moving one field backward and one field sideward. When it arrives at the starting position of the opponents non-pawns (marked by a thin frame), it promotes to anything except a king.")
	(image Red "images\Chess\RPawn2.bmp" Yellow "images\Chess\YPawn2.bmp" Green "images\Chess\GPawn2.bmp" Blue "images\Chess\BPawn2.bmp")
	(moves
		(Pawn-capture s e)
		(Pawn-capture s w)
		(Pawn-move s)
		(En-Passant e s)
		(En-Passant w s)
	)
)

(define RealPieces
	(piece
		(name Knight)
		(help "Knight: moves like 'L'")
		(description "Knight:\The only piece that leaps over other pieces. It moves like an 'L', two steps in one direction and one orthogonally. That makes it a short range piece. It is less powerful at the edge of the board.")
		(image Red "images\Chess\RKnight.bmp" Yellow "images\Chess\YKnight.bmp" Green "images\Chess\GKnight.bmp" Blue "images\Chess\BKnight.bmp")
		(moves
			(leap n e)
			(leap n w)
			(leap s e)
			(leap s w)
			(leap e n)
			(leap e s)
			(leap w n)
			(leap w s)
		)
	)

	(piece
		(name Bishop)
		(help "Bishop: slides diagonally")
		(description "Bishop:\A Bishop moves any number of squares on a diagonal. It may not leap over other pieces.")
		(image Red "images\Chess\RBishop.bmp" Yellow "images\Chess\YBishop.bmp" Green "images\Chess\GBishop.bmp" Blue "images\Chess\BBishop.bmp")
		(moves
			(slide2 n e)
			(slide2 n w)
			(slide2 s e)
			(slide2 s w)
		)
	)

	(piece
		(name Rook)
		(help "Rook: slides orthogonally")
		(description "Rook:\A Rook moves any number of squares orthogonally on a rank or a file. It may not leap over other pieces.")
		(image Red "images\Chess\RRook.bmp" Yellow "images\Chess\YRook.bmp" Green "images\Chess\GRook.bmp" Blue "images\Chess\BRook.bmp")
		(attribute never-moved? true)
		(moves
			(rook-slide n)
			(rook-slide s)
			(rook-slide e)
			(rook-slide w)
		)
	)

	(piece
		(name Queen)
		(help "Queen: moves like Bishop or Rook")
		(description "Queen:\A Queen moves like a Bishop or a Rook.")
		(image Red "images\Chess\RQueen.bmp" Yellow "images\Chess\YQueen.bmp" Green "images\Chess\GQueen.bmp" Blue "images\Chess\BQueen.bmp")
		(moves
			(slide n)
			(slide s)
			(slide e)
			(slide w)
			(slide2 n e)
			(slide2 n w)
			(slide2 s e)
			(slide2 s w)
		)
	)

	(piece
		(name King)
		(help "King: moves one step in any direction, not into check, may castle")
		(description "King:\A king moves one step in any direction, but never into check. A king that has not moved yet may castle with a rook that has not moved either. The rook moves next to the king and the king leaps over the rook. The king may not castle through or out of check.")
		(image Red "images\Chess\RKing.bmp" Yellow "images\Chess\YKing.bmp" Green "images\Chess\GKing.bmp" Blue "images\Chess\BKing.bmp")
		(attribute never-moved? true)
		(moves
			(king-shift n)
			(king-shift s)
			(king-shift e)
			(king-shift w)
			(king-shift2 n e)
			(king-shift2 n w)
			(king-shift2 s e)
			(king-shift2 s w)
			(O-O-S)
			(O-O-L)
			(O-O-O-S)
			(O-O-O-L)
		)
	)
)

(define Pieces
	(piece
		(name UpPawn)
		(Up-Pawn)
	)
	(piece
		(name DownPawn)
		(Down-Pawn)
	)
	(RealPieces)
)

(define BoardSetup3
	(Red
		(UpPawn a5 b5 c5 d5 e5 f5 g5 h5)
		(DownPawn a3 b3 c3 d3 e3 f3 g3 h3)
		(Knight b4 g4)
		(Bishop c4 f4)
		(Rook a4 h4)
		(Queen d4)
		(King e4)
	)
	(Green
		(UpPawn a12 b12 c12 d12 e12 f12 g12 h12)
		(DownPawn a10 b10 c10 d10 e10 f10 g10 h10)
		(Knight b11 g11)
		(Bishop c11 f11)
		(Rook a11 h11)
		(Queen d11)
		(King e11)
	)
	(Blue
		(UpPawn a19 b19 c19 d19 e19 f19 g19 h19)
		(DownPawn a17 b17 c17 d17 e17 f17 g17 h17)
		(Knight b18 g18)
		(Bishop c18 f18)
		(Rook a18 h18)
		(Queen d18)
		(King e18)
	)
)

(define BoardSetup4
	(Red
		(UpPawn a5 b5 c5 d5 e5 f5 g5 h5)
		(DownPawn a3 b3 c3 d3 e3 f3 g3 h3)
		(Knight b4 g4)
		(Bishop c4 f4)
		(Rook a4 h4)
		(Queen d4)
		(King e4)
	)
	(Yellow
		(UpPawn a12 b12 c12 d12 e12 f12 g12 h12)
		(DownPawn a10 b10 c10 d10 e10 f10 g10 h10)
		(Knight b11 g11)
		(Bishop c11 f11)
		(Rook a11 h11)
		(Queen d11)
		(King e11)
	)
	(Green
		(UpPawn a19 b19 c19 d19 e19 f19 g19 h19)
		(DownPawn a17 b17 c17 d17 e17 f17 g17 h17)
		(Knight b18 g18)
		(Bishop c18 f18)
		(Rook a18 h18)
		(Queen d18)
		(King e18)
	)
	(Blue
		(UpPawn a26 b26 c26 d26 e26 f26 g26 h26)
		(DownPawn a24 b24 c24 d24 e24 f24 g24 h24)
		(Knight b25 g25)
		(Bishop c25 f25)
		(Rook a25 h25)
		(Queen d25)
		(King e25)
	)
)

(game
	(title "Tricylinder")
	(description "The n-line cylinder can be built up from n ordinary chess boards by identifying the last rank of the i'th board with the first rank of the (i+1)'th board. Using cylindrical instead of ordinary chess boards yields the n-line torus.")
	(history "Invented by Manuel Hohmann in 2002, programmed in May 2003 with the advise of Ingo Alth√∂fer.")
	(players Red Green Blue Yellow)
	(turn-order Red Green Blue)
	(pass-turn false)
	(board
		(image "images\Chess\FCyl8x21.bmp")
		(Board3)
		(links n (a21 a1) (b21 b1) (c21 c1) (d21 d1) (e21 e1) (f21 f1) (g21 g1) (h21 h1))
		(links s (a1 a21) (b1 b21) (c1 c21) (d1 d21) (e1 e21) (f1 f21) (g1 g21) (h1 h21))
	)
	(board-setup (BoardSetup3))
	(Pieces)
	(win-condition (Red Green Blue) (and (total-piece-count 2 King) (pieces-remaining 1 King)))
	(draw-condition (Red Green Blue) stalemated)
)

(variant
	(title "Tritorus")
	(turn-order Red Green Blue)
	(board
		(image "images\Chess\Torus8x21.bmp")
		(Board3)
		(links n (a21 a1) (b21 b1) (c21 c1) (d21 d1) (e21 e1) (f21 f1) (g21 g1) (h21 h1))
		(links s (a1 a21) (b1 b21) (c1 c21) (d1 d21) (e1 e21) (f1 f21) (g1 g21) (h1 h21))
		(links e (h1 a1) (h2 a2) (h3 a3) (h4 a4) (h5 a5) (h6 a6) (h7 a7)
			(h8 a8) (h9 a9) (h10 a10) (h11 a11) (h12 a12) (h13 a13) (h14 a14)
			(h15 a15) (h16 a16) (h17 a17) (h18 a18) (h19 a19) (h20 a20) (h21 a21))
		(links w (a1 h1) (a2 h2) (a3 h3) (a4 h4) (a5 h5) (a6 h6) (a7 h7)
			(a8 h8) (a9 h9) (a10 h10) (a11 h11) (a12 h12) (a13 h13) (a14 h14)
			(a15 h15) (a16 h16) (a17 h17) (a18 h18) (a19 h19) (a20 h20) (a21 h21))
	)
	(board-setup (BoardSetup3))
	(win-condition (Red Green Blue) (and (total-piece-count 2 King) (pieces-remaining 1 King)))
	(draw-condition (Red Green Blue) stalemated)
)

(variant
	(title "Tetracylinder")
	(turn-order Red Yellow Green Blue)
	(board
		(image "images\Chess\FCyl8x28.bmp")
		(Board4)
		(links n (a28 a1) (b28 b1) (c28 c1) (d28 d1) (e28 e1) (f28 f1) (g28 g1) (h28 h1))
		(links s (a1 a28) (b1 b28) (c1 c28) (d1 d28) (e1 e28) (f1 f28) (g1 g28) (h1 h28))
	)
	(board-setup (BoardSetup4))
	(win-condition (Red Green Blue Yellow) (and (total-piece-count 3 King) (pieces-remaining 1 King)))
	(draw-condition (Red Green Blue Yellow) stalemated)
)

(variant
	(title "Tetratorus")
	(turn-order Red Yellow Green Blue)
	(board
		(image "images\Chess\Torus8x28.bmp")
		(Board4)
		(links n (a28 a1) (b28 b1) (c28 c1) (d28 d1) (e28 e1) (f28 f1) (g28 g1) (h28 h1))
		(links s (a1 a28) (b1 b28) (c1 c28) (d1 d28) (e1 e28) (f1 f28) (g1 g28) (h1 h28))
		(links e (h1 a1) (h2 a2) (h3 a3) (h4 a4) (h5 a5) (h6 a6) (h7 a7)
			(h8 a8) (h9 a9) (h10 a10) (h11 a11) (h12 a12) (h13 a13) (h14 a14)
			(h15 a15) (h16 a16) (h17 a17) (h18 a18) (h19 a19) (h20 a20) (h21 a21)
			(h22 a22) (h23 a23) (h24 a24) (h25 a25) (h26 a26) (h27 a27) (h28 a28))
		(links w (a1 h1) (a2 h2) (a3 h3) (a4 h4) (a5 h5) (a6 h6) (a7 h7)
			(a8 h8) (a9 h9) (a10 h10) (a11 h11) (a12 h12) (a13 h13) (a14 h14)
			(a15 h15) (a16 h16) (a17 h17) (a18 h18) (a19 h19) (a20 h20) (a21 h21)
			(a22 h22) (a23 h23) (a24 h24) (a25 h25) (a26 h26) (a27 h27) (a28 h28))
	)
	(board-setup (BoardSetup4))
	(win-condition (Red Green Blue Yellow) (and (total-piece-count 3 King) (pieces-remaining 1 King)))
	(draw-condition (Red Green Blue Yellow) stalemated)
)
